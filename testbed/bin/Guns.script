controller Boss1
{
	// member variables
	time = 3;

	// emitters - all these values are offsets from controller values
	left = emitter Flower(-128, 0, 0);
	right = emitter Flower(128, 0, 0);
	up = emitter Flower(0, -128, 0);
	down = emitter Flower(0, 128, 0);
	
	// events
	Damaged = event(newHealth)
	{
	}


	// states
	Stage1 = state
	{
		wait(time);
		$left.This_X += {64, 5};
		$right.This_X -= {64, 5};
		wait(5);
		goto Stage2;
	}

	Stage2 = state
	{
		wait(time);
		$left.This_X -= {64, 5};
		$right.This_X += {64, 5};
		wait(5);
		goto Stage1;
	}
}

emitter Test
{
	time = 0.1;
	Stage1 = state 
	{
		x = 5;
		fireA bullet(This_Angle, 100);
		wait(time);
	}

	Stage2 = state
	{
		fireA bullet(This_Angle + 90, 100);
		wait(time);
	}
}

emitter Flower
{
	// member variables
	fl_angle = 0;
	fl_speed = 2;
	fl_dir = 1;

	// affectors
	gravity = affector Gravity(98);

	// functions
	death = function(time)
	{
		$blue = 0.2;
		$alpha = {0, time};
		wait(time);
		die();
	}

	petal_o = function(dir)
	{
		fireA bullet($angle + 5 * dir, 70) : petal_i(dir);
		$blue = 0.2;
		wait(0.4);
		$angle += {60 * dir, 2};
		wait(1.7);

		i = 0;
		while(i < 20)
		{
			i++;
			fireA bullet(rand(360), 100) : death(0.5), gravity;
		}

		die();
	}

	petal_i = function(dir)
	{
		$blue = 0.6;
		$green = 0.2;
		wait(0.2);
		$angle += {60 * dir, 2};
		wait(1.2);
		die();
	}

	// states
	Grow = state
	{
		fireA bullet(fl_angle + 40, 70) : petal_o(1);
		fireA bullet(fl_angle + 80, 70) : petal_o(-1);

		fireA bullet(fl_angle + 100, 70) : petal_o(1);
		fireA bullet(fl_angle + 140, 70) : petal_o(-1);

		fireA bullet(fl_angle + 160, 70) : petal_o(1);
		fireA bullet(fl_angle + 200, 70) : petal_o(-1);

		fireA bullet(fl_angle + 220, 70) : petal_o(1);
		fireA bullet(fl_angle + 260, 70) : petal_o(-1);

		fireA bullet(fl_angle + 280, 70) : petal_o(1);
		fireA bullet(fl_angle + 320, 70) : petal_o(-1);

		fireA bullet(fl_angle + 340, 70) : petal_o(1);
		fireA bullet(fl_angle + 20, 70) : petal_o(-1);

		wait(0.1);

		fl_speed += 0.2 * fl_dir;
		if (fl_speed < 1 || fl_speed > 10)
		{
			fl_dir *= -1;
		}

		fl_angle += fl_speed;
		if (fl_angle >= 360)
		{
			fl_angle -= 360;
		}

		fl_angle++;
	}

}
