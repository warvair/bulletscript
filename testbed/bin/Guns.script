gun Abstract
{
	m_speed = 60;

	Func1 = function(time, speed, angle)
	{
		wait(time);
		$angle = {$angle + angle, 2};
		wait(2);
		fireA bullet(-angle, speed) : Func2(2, speed, angle + 50);
		$fade = {0, 2};
		wait(2);
		die();

	}

	Func2 = function(time, speed, angle)
	{
		wait(time);
		$angle = {$angle - angle, 2};
		wait(2);
		fireA bullet(angle, speed) : Func1(2, speed, angle - 50);
		$fade = {0, 2};
		wait(2);
		die();
	}

	Main = state
	{
		fireA bullet(170, m_speed) : Func1(1.2, m_speed, 120);
		fireA bullet(190, m_speed) : Func2(1.2, m_speed, 120);
		wait(0.2);
	}
}

gun Beam
{
	time = 0.5;

	debris = function()
	{
		wait(0.3);
		$fade = {0, 0.6};
		wait(0.6);
		die();
	}

	grow = function(dir, time)
	{
		$start = {180 + 180 * dir, 0.3};
		$end = {180, 0.3};
		wait(time);

		a = 0;
		loop (10)
		{
			fireA bullet(a * 36, 80) : debris();
			a++;
		}

		$fade = {0, 0.3};
		$width = {300, 0.1};
		$height = {300, 0.1};
		$iwidth = {$iwidth + 100, 0.1};
		$iheight = {$iheight + 100, 0.1};
	}

	// States
	Emit = state
	{
		arc area(64, 64, 90, 90, 32) : grow(-1, time);
		arc area(64, 64, 270, 270, 32) : grow(1, time);

		wait(time);
		ship1 unit();

		goto Stable;
	}

	Stable = state
	{
		wait(10);
	}
}

gun Blaster
{
	Main = state
	{
		i = 0;
		loop(11)
		{
			fireA bullet(160 + i * 4, 200);
			i++;
		}
		wait(1);
	}
}
