controller Boss4
{
	tracker = emitter Track(0, -66);
	hell = emitter Hell(0, -66);
//	bomb1 = emitter SmartBomb(33, 14, 135);
//	bomb2 = emitter SmartBomb(-33, 14, 225);
//	arc1 = emitter AttackArc(51, 41);
//	arc2 = emitter AttackArc(-51, 41);

	On50 = event()
	{
		goto $tracker.Erratic;
	}

	On25 = event()
	{
		suspend $tracker;
		signal $hell;
	}

	Setup = state
	{
		suspend $hell;
		goto Go;
	}

	Go = state
	{
		wait(10);
	}
}

emitter Track
{
	Stable = state
	{
		fireT bullet(BALL, Player_X, Player_Y, 0, 350);
		wait(0.1);
	}

	Erratic = state
	{
		fireT bullet(BALL, Player_X, Player_Y, rand(10), 350);
		wait(0.1);
	}
}

emitter SmartBomb
{
	// members
	speed = 120;
	wait_time = 3;

	// functions
	expand = function()
	{
		$width = {400, 0.4};
		$height = {400, 0.4};
		$iwidth += {50, 0.4};
		$iheight += {50, 0.4};
		wait(0.3);
		$alpha = {0, 0.1};
		wait(0.1);
		die;
	}

	debris = function()
	{
		$alpha = {0, 1};
		wait(1);
		die;
	}

	delayExplode = function(time)
	{
		wait(time);
		arc area(30, 30, 0, 360, 20) : expand();
		
		i = 0;
		while (i < 20)
		{
			fireA bullet(BALL, rand(360), 35) : debris();
			i++;
		}
		die;
	}

	// states
	Fire = state
	{
		fireA bullet(BALL, This_Angle, speed) : delayExplode(1.5);
		wait(wait_time);
	}

}

emitter AttackArc
{
	m_dir = 0;

	Spin = function(dir)
	{
		$width += {20, 2};
		$height += {20, 2};
		wait(2.5);

		$start += {dir * 135, 0.5};
		$end += {dir * 135, 0.5};
		wait(0.5);
//		$speed = 200;
		wait(5);
		die;
	}

	Emit = state
	{
		arc area(48, 48, -45, 45, 20) : Spin(m_dir), &kill;
		wait(10);
	}
}

emitter Hell
{
	count = 15;
	
	Swirl1 = function(c, t, a)
	{
		$red = c;
		$green = 0.5 + (1 - c) * 0.5;
		$blue = 0;
		wait(t);
		$angle = {270 + a, 1};
		wait(2);
//		$red = {0, 1};
//		$blue = {1, 1};
		$angle -= {100, 0.5};
		wait(0.75);
//		$red = {1 - c, 0.5};
		$angle -= {100, 0.5};
		wait(1.25);
		$angle += {100, 0.5};
	}

	Swirl2 = function(c, t, a)
	{
		$red = c;
		$green = 0.5 + (1 - c) * 0.5;
		$blue = 0;
		wait(t);
		$angle = {90 - a, 1};
		wait(2);
		$red = {0, 1};
		$blue = {1, 1};
		$angle += {100, 0.5};
		wait(0.75);
		$red = {1 - c, 0.5};
		$angle += {100, 0.5};
		wait(1.25);
		$angle -= {100, 0.5};
	}

	Emit = state
	{
		i = 0;
		c1 = count - 1;
		while (i < count)
		{
			fireA bullet(BALL, 100 + i, 320) : Swirl1(i / c1, 0.5 + 0.5 * (i / c1), i * 2);
			fireA bullet(BALL, 260 - i, 300) : Swirl2(i / c1, 0.5 + 0.5 * (i / c1), i * 2);
			i++;
		}
		wait(0.1);
	}
}
