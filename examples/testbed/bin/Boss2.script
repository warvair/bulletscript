controller Boss2
{
	centre1 = emitter Beam1;
	centre2 = emitter Beam1;
	
	innerl = emitter Beam1(-54, -5);
	innerr = emitter Beam1(54, -5);

	outerl = emitter Beam2(-98, 4, 8);
	outerr = emitter Beam2(98, 4, -8);

	sidel = emitter Beam3(-138, -2, 13);
	sider = emitter Beam3(138, -2, -13);

	Setup = state
	{
		// Set up member variables for emitters instances, so that we can use the same emitter
		// definition, to simplify things.
		$centre1.m_size = 200;
		$centre1.m_proj = 25;
		$centre2.m_size = 200;
		$centre2.m_proj = 25;

		$innerl.m_size = 300;
		$innerl.m_proj = 14;
		$innerr.m_size = 300;
		$innerr.m_proj = 14;
		
		goto Go;
	}

	Go = state
	{
		// Change the emitter angle.  The beams are anchored to the angle, so this will change
		// the beams' angles.
		$centre1.This_Angle += {45, 2};
		$centre2.This_Angle -= {45, 2};
		$innerl.This_Angle -= {36, 2};
		$innerr.This_Angle += {36, 2};
		wait(2);

		$centre1.This_Angle -= {90, 4};
		$centre2.This_Angle += {90, 4};
		$innerl.This_Angle += {72, 4};
		$innerr.This_Angle -= {72, 4};
		wait(4);

		$centre1.This_Angle += {45, 2};
		$centre2.This_Angle -= {45, 2};
		$innerl.This_Angle -= {36, 2};
		$innerr.This_Angle += {36, 2};
		wait(2);
	}
}

emitter Beam1
{
	m_size = 0;
	m_proj = 0;

	Resize = function(size)
	{
		while(1)
		{
			$height += {size, 3};
			wait(3);
			$height -= {size, 3};
			wait(3);
		}
	}

	Emit = state
	{
		// Emit one beam, anchored to the emitter, and go into a loop
		quadProj area(m_proj, 25, 100, 180) : &kill, &angle, Resize(m_size);
		goto Stable;
	}

	Stable = state
	{
		wait(10);
	}
}

emitter Beam2
{
	Resize = function()
	{
		while(1)
		{
			wait(3);
			$height += {400, 0.7};
			wait(0.7);
			$height -= {400, 4};
			wait(4);
		}
	}

	Emit = state
	{
		quadProj area(13, 25, 0, 180) : &kill, &angle, Resize();
		goto Stable;
	}

	Stable = state
	{
		wait(10);
	}
}

emitter Beam3
{
	Resize = function()
	{
		while(1)
		{
			wait(3);
			$height += {400, 0.7};
			wait(0.7);
			$height -= {400, 4};
			wait(4);
		}
	}

	Emit = state
	{
		quadProj area(5, 25, 0, 180) : &kill, &angle, Resize();
		goto Stable;
	}

	Stable = state
	{
		wait(10);
	}
}